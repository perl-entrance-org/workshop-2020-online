# Perl 入学式 第 2 回

## 構文基礎編

---

## 諸注意

- 講義中の疑問点は Discord で質問して下さい。サポーターが適宜回答やアドバイスを行える様にスタンバイしています。

- うまくプログラムが動かない、分からない時は Discord #講義部屋 でサポーターにヘルプを要請してください。Discord のテキスト及び音声チャットにて個別にサポートします。

- <a href="https://discord.com/" target="_blank">Discord | 会話や交流が楽しめるプラットフォーム</a>

---

## 本日の内容

- 前回の復習
- コメント
- スカラー変数
- 四則演算
- 条件分岐（ if 文）
- 比較演算子（ <, >, ==, !=, eq, ne, gt, lt ）
- 論理演算子（ &&, || ）

---

# 前回の復習

---

## 前回の復習

### おまじない

```perl
#!/usr/bin/env perl
use strict;
use warnings;
```

以下、この資料のサンプルプログラムでは紙面の都合上｢おまじない｣を省略します。

皆さんは**必ず記述してください**。

---

## 前回の復習

### 練習問題 (nickname.pl)

"my nickname is ○○" と表示されるプログラムを作ってみましょう。

1. nickname.pl というファイルを作成してください。

   - nickname.pl に先程説明した "おまじない" を忘れないこと！

   - プログラムは上から下へ順番に処理されていきます。

1. nickname 自己紹介文を画面に表示させるプログラムを 2 に続けて書きましょう。

ヒント 画面に文字（文）を表示させる命令は print です。

---

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

---

# コメント

---

## コメント

```perl
print "Hello World!";   # -- ここがコメント部 --

# print "Hello! Perl Entrance!"; 行頭に # があるため、printは実行されない
```

- 各プログラム言語にはプログラムをわかりやすくするために、コメントを入れる仕組みがあります。

- Perl では、 `#` より右にある文字列は全てコメントとして扱われます。

- コメントは実行されません。

- この資料の中でも、 コメントを使ってプログラムに補足を行います。

---

# スカラー変数

---

## スカラー変数

### 変数とは?

- ｢**<ruby>変数<rt>へんすう</rt></ruby>**｣は、いわば｢データを格納できる箱｣です。

- Perl の変数には、｢スカラ変数｣、｢<ruby>配列<rt>はいれつ</rt></ruby>｣、｢ハッシュ(<ruby>連想配列<rt>れんそうはいれつ</rt></ruby>)｣という 3 つの種類があります。

---

## スカラー変数

### 変数とは?

3 種類の変数は、それぞれの変数の型を示す **シジル(sigil)** という記号で区別されます。

1. スカラ変数(`$`)：1 つの要素を格納する。

1. 配列(`@`)：複数の要素を「順番に」格納する。

1. ハッシュ(`%`)：複数の要素を「名前 => 値」の組み合わせで格納する。

配列とハッシュ（連想配列）は第 3 回で解説予定です。

---

## スカラー変数

### スカラー変数を利用する

```perl
my $foo;    # 変数を宣言する

$foo = 1;   # 右辺の 1 を左辺の $foo に入れる

print "$foo\n";
```

- 変数を初めて使うときは、先頭に `my` をつけます。

- 変数が利用できるようにすることを「**変数を宣言**する」といいます。

---

## スカラー変数

### スカラー変数を利用する

```perl
my $foo;    # 変数を宣言する

$foo = 1;   # 右辺の 1 を左辺の $foo に入れる

print "$foo\n";
```

- スカラ変数の先頭には、`$foo` のように `$` をつけます。 `$` は`スカラ(scalar)`の `S` 、と覚えるとよいでしょう。

- `=` という記号(代入演算子)を使うことで、「右辺の値」を「左辺のスカラー変数 `$foo` 」に入れる(代入する)ことができます。

---

## スカラー変数

### スカラー変数を利用する

```perl
my $foo;    # 変数を宣言する
$foo = 1;          # 右辺の 1 を左辺の $foo に入れる
print "$foo\n";    # 1

$foo = "Hello World";   # $foo は宣言済みなので my は不要
                        # 右辺の "Hello World" が 左辺の $foo に入る
print "$foo\n";         # Hello World
```

- 宣言した変数を 2 回目以降に使うときは、 `my` は不要です。`my` は変数を宣言する初回のみ必要です。

- 値が入っているスカラー変数に別の値を代入すると、中身が上書きされます

---

## スカラー変数

### 宣言と代入を同時に行う

```perl
my $foo = 1;    # 変数の宣言と同時に代入する
print "$foo\n"; # 1
```

変数は、宣言と代入を同時に行うことができます。

---

## スカラー変数

### 仮の変数名（メタ構文変数）

```perl
my $foo;
my $bar = 1;
$bar = "Hello World";
```

- プログラム言語の参考書には以下の単語が変数名として利用されることがあります。

  - foo, bar, baz

  - hoge, piyo, fuga

---

## スカラー変数

### 仮の変数名（メタ構文変数）

- これら「意味のない名前」をつけることで、「サンプル用」「例示用」の変数ということを明示するためです。

- `foo`, `bar`, `buz` このような単語を使った変数を「**メタ構文変数**」と呼称します。

- これらの変数名は現場のプログラムでは利用しないようにしましょう。

---

## スカラー変数

### クォーテーションによる表示の違い

```perl
my $foo = 1;

print "$foo\n"; # ダブルクォーテーションで囲うと「1」が表示される

print '$foo\n'; # シングルクォーテーションで囲うと「$foo\n」が表示される
```

`print` を使うことで変数を表示することができます。

---

## スカラー変数

### クォーテーションによる表示の違い

- 変数の中身を表示する場合は、 `" "(ダブルクォーテーション)`の中に変数を書きましょう。

  - `" "` の中に変数を書くと、その変数の中身が表示されます。

  - `' '` の中では、変数の中身ではなく、書いてあるままに表示されます。

---

## スカラー変数

### エラーメッセージ

```perl
#!/usr/bin/env perl
use strict;
use warnings;

my $foo = "Hello"  # "hello"の後にセミコロンがない
print "$foo\n";
```

エラーメッセージ

```perl
syntax error at prog.pl line 6, near "print"
Execution of prog.pl aborted due to compilation errors.
```

---

## スカラー変数

### エラーメッセージ

- よくありがちなエラーとして、行末の「セミコロン `;` を忘れる」というものがあります。

- さきほどの場合、6 行目の print の周辺でシンタックスエラーが発生しているという意味になります

---

## スカラー変数

### エラー文は怖くない

Perl に限らず、プログラムのエラーメッセージのほとんどは英語で表示されます。

最初は戸惑うかもしれませんが、エラーメッセージ中にある行番号を手がかりに**直前・直後の行**でおかしいところを探してみましょう。

また、エラーメッセージそのものを Google などで検索したり、翻訳することでエラー解決への手がかりを掴むこともできます。

---

## スカラー変数

### エラー文は怖くない

エラーが出るのは悪いことではありません。直すべき場所を明示してくれるありがたい指針です。

**エラーを出さずにプログラムを習得することはできません。**

落ち着いて対処していきましょう。

---

## スカラー変数

### 練習問題 1 (scalar_practice1.pl)

- 1 から 9 までの好きな数字をスカラー変数に格納し、それらの値を表示するプログラム `scalar_practice1.pl` を作成しよう！

  - `$number` 好きな数字を格納する変数

- 「お約束」の 3 行を忘れずに！

---

## スカラー変数

### 練習問題 2 (scalar_practice2.pl)

- 好きなプログラミング言語の名前をスカラ変数に格納し、それらの値を表示するプログラム `scalar_practice2.pl` を作成しよう！

  - `$language` 好きなプログラミング言語名

---

## スカラー変数

### 練習問題 3 (scalar_practice3.pl)

- Perl 入学式の情報をスカラー変数に格納し、それらの値を表示するプログラム `scalar_practice3.pl` を作成しよう！

  - `$seminar_name` この勉強会の名前
  - `$date` 今日の日付

---

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

## 休憩 ＆ 質問 ＆ 雑談 タイム<br>（ 5 〜 10 分）

---

## （コラム）Perl5 のバージョンの歴史

- Perl 4 以前は前史として、既に周囲に環境が存在しないと思って良いです。

- 1994 年、Perl5 (Perl 5.000) が登場しました。

- 2000 年、Perl 5.6 が登場。この頃からインターネットやウェブというものが徐々に一般に普及していきます。

- 2002 年、Perl 5.8 が登場。国際化対応や今につながる様々な機能が搭載されます。5.8 時代が長かったため、多くのサービスで長きにわたって使われました。

参考: <a href="https://speakerdeck.com/anatofuz/regasiperlbirudo-xian-dai-nisu-ruperl-1-2-dot-0toperl6" target="_blank">レガシー Perl ビルド 〜現代に蘇る Perl[1..5].0 と Perl6〜</a>

---

## （コラム）Perl5.8 以降のバージョンの歴史

- 2007 年 : Perl 5.10 が登場。 後方互換性を維持した構文拡張等。
  以降、1 年に 1 回のマイナーバージョンアップ

- 2018 年 : Perl 5.28 が登場。

- 2019 年 : Perl 5.30 が登場。

- 2020 年 : Perl 5.32 が登場。 現在の最新の安定版です。

---

# 四則演算と<br>文字列連結

---

## 四則演算と文字列連結

### 基本的な演算子(足し算・引き算)

```perl
my $foo = 4;
my $bar = 2;
my $result;

# 足し算 +
$result = $foo + $bar;    # 足し算 して $result に代入する
print "$foo + $bar = ";   # 式の表示だけ。改行はしない。
print "$result\n";        # 上の式に続けて $result を表示する

# 引き算 -
$result = $foo - $bar;    # 引き算 して $result に代入する
print "$foo - $bar = ";   # 表示だけ。改行はしない。
print "$result\n";        # 上の式に続けて $result を表示する
```

算数と同じ + と - が利用できます。

---

## 四則演算と文字列連結

### 基本的な演算子(掛け算・割り算)

```perl
my $foo = 4;
my $bar = 2;
my $result;

# 掛け算 * （アスタリスク）
$result = $foo * $bar;    # 掛け算 して $result に代入する
print "$foo * $bar = ";   # 表示だけ。改行はしない。
print "$result\n";        # 上の式に続けて $result を表示する

# 割り算 / （スラッシュ）
$result = $foo / $bar;    # 割り算 して $result に代入する
print "$foo / $bar = ";   # 表示だけ。改行はしない。
print "$result\n";        # 上の式に続けて $result を表示する
```

算数の掛ける `×` は `*`、割る `÷` は `/` と、算数と異なる記号を使います。

---

## 四則演算と文字列連結

### 基本的な演算子(べき乗・剰余)

```perl
my $foo = 4;
my $bar = 2;
my $result;

# べき乗 **
$result = $foo**$bar;       # 4 の 2乗 を $result に代入する
print "$foo ** $bar = ";    # 式の表示だけ。改行はしない。
print "$result\n";          # 上の式に続けて $result を表示する

# 剰余 %
$result = $foo % $bar;     # 4 を 2 で割った余りを $result に代入する
print "$foo % $bar = ";    # 表示だけ。改行はしない。
print "$result\n";         # 上の式に続けて $result を表示する
```

---

## 四則演算と文字列連結

### 剰余の使い所

<ruby>**剰余**<rt>じょうよ</rt><ruby>は、初めて聞く言葉かもしれません。利用例を解説します。

```perl
print 100 % 7;    # 100 / 7 の余りである 2
```

例えば「今日の 100 日後の曜日」を求めるとき、100 日分カレンダーを辿るのは大変です。

---

## 四則演算と文字列連結

### 剰余の使い所

1 週間は 7 日なので、7 日後は同じ曜日となります。14 日後も同じ曜日となります。21 日、28 日、35 日... 7 の倍数日後は同じ曜日になります。

これを利用し、100 日 を 7 日 で割ってみます。剰余は 2 となります。

明日の曜日と同じであれば剰余は 1、 明後日と同じ曜日であれば 2、 と簡単に計算が可能です。

---

## 四則演算と文字列連結

### 練習問題(keisan1.pl)

以下の問題を順番に書いて表示してみよう。

1. 変数 `$foo` に 10, `$bar` に 3 を代入し、それぞれを画面に表示させる。

1. 変数 `$foo` から 変数 `$bar` を引いた数を画面に表示させる。

1. 変数 `$foo` を 変数 `$bar` で割った時の余りを表示させる。

---

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

## 回答 ＆ 休憩 ＆ 質問 ＆ 雑談 タイム<br>（ 5 〜 10 分）

---

## 四則演算と文字列連結

### 代入演算子の性質

```perl
my $foo = 4;

$foo = $foo * 3;  # 右辺の計算結果( 4 * 3 )が、左辺の $foo に代入される

print "$foo\n";   # 12
```

- 代入演算子 `=` は、算数のイコールとは性質が異なります。

  - 算数　　　：左辺と右辺が等しいことを示す

  - プログラム：右辺の計算結果を左辺に代入する

- 上記の例では右辺の「 `$foo * 3` 」つまり 12 が `=` の左辺 `$foo` に代入されます。

---

## 四則演算と文字列連結

### 省略記法

代入演算子（ `=` ）を利用して、変数の中身を書き換える処理は多いため、専用の記法が用意されています。

```perl
my $bar = 100;
my $foo = 50;

$bar += 1;         # $bar = $bar + 1;と同じ
print "$bar\n";    # 101
$bar -= 1;         # $bar = $bar - 1;と同じ
print "$bar\n";    # 100

$bar *= 2;         # $bar = $bar * 2;と同じ
print "$bar\n";    # 200
$bar /= $foo;     # $bar = $bar / $foo と同じ
print "$bar\n";    # 4
```

---

## 四則演算と文字列連結

### インクリメント

1 ずつ加算することを「インクリメント」といいます。この処理も多用するので、専用の記法が用意されています。

```perl
my $foo = 4;

$foo = $foo + 1;# 代入演算子を利用
print "$foo\n"; # 5

$foo += 1;      # 省略記法
print "$foo\n"; # 6

++$foo;         # 前置インクリメント
print "$foo\n"; # 7

$foo++;         # 後置インクリメント
print "$foo\n"; # 8
```

---

## 四則演算と文字列連結

### インクリメント

前置インクリメントと後置インクリメントの違いについては[こちら](https://tutorial.perlzemi.com/blog/20180224151948.html)を参照してください。

---

## 四則演算と文字列連結

### 練習問題(keisan2.pl)

以下の問題を順番に書いて表示してみよう。

1. 変数 `$num` に 8 を代入し画面に表示させる。

2. 省略記法を使って、変数 `$num` を 10 倍し、画面に表示させる

3. `$num` を 3 回インクリメントし、画面に表示させる。（正しくプログラムが書けていれば、83 と表示するはずです。）

---

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

---

## 休憩 ＆ 質問 ＆ 雑談 タイム<br>（ 5 〜 10 分）

---

## 四則演算と文字列連結

### 演算の順番

```perl
my $foo = 2 + 4 * 3;
print "$foo\n";    # 14

my $bar = ( 2 + 4 ) * 3;
print "$bar\n";    # 18
```

- 演算の順序は、 `( )` を付けることで変更することができます。

- 通常の算数と同じく、Perl も「足し算･引き算」より「掛け算･割り算」の優先順位が高くなっています。

- 下の例では `( )` を付けることで優先順位を変え、「足し算」を先に計算させています。

---

## 四則演算と文字列連結

### 文字列連結

```perl
my $foo = 'hoge';
my $bar = 123;

print "$foo$bar\n";         # hoge123

print $foo . $bar . "\n";   # hoge123

my $buz = $foo . $bar;
print $buz . "\n";          # hoge123
```

- `.` (ドット)で文字列や変数を連結することができます。

- 連結した文字列や変数を、別の変数に代入することも可能です。

---

## 四則演算と文字列連結

### 練習問題(strconcat.pl)

以下のプログラムをコピペして、正しく出力される様に変数を連結するプログラム `strconcat.pl` を続けて書いてみましょう。

```perl
my $str1 = "名前は";
my $str2 = "猫である。";
my $str3 = "まだ無い。";
my $str4 = "吾輩は";
```

---

## 休憩 ＆ 質問 ＆ 雑談 タイム<br>（ 5 〜 10 分）

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

---

## 四則演算と文字列連結

### 数値演算と文字列連結は分ける

演算の優先順位と結合方向(演算の向き)は perldoc の [perlop](http://perldoc.jp/docs/perl/5.22.1/perlop.pod#Operator32Precedence32and32Associativity) の項に詳細が記載されています。

ただし、これを覚えるのも、他の人に読んでもらうのも大変な労力です。

`( )`で演算の順番を明確に示す、数値演算と文字列の連結は分けて書くなど、わかりやすい記述を心がけましょう。

---

## 四則演算と文字列連結

### 数値演算と文字列連結の混合

```perl
my $now                 = 2020;
my $start_perl_entrance = 2012;
print "Perl入学式は開講" . $now - $start_perl_entrance . "年です\n";
# -2012年です
```

上のプログラムは、数値演算と文字列連結を混ぜた場合の「アンチパターン（良くない例）」です。

「-2012 年です」と、おかしな結果になります。これは演算子の優先順位に即した結果です。

---

## 四則演算と文字列連結

### 数値演算と文字列連結の混合

1. まず 「`Perl入学式は開講`」（文字） と 「`2020`」（数字） が文字連結されます。（結果：「今年は開講 2020」）

1. 次に、「`今年は開講2020`」（文字） から 「`2012`」（数字）を引き算しようとし、おかしな結果になります。

---

## 四則演算と文字列連結

### 数値演算と文字列連結は分ける

文字と数値計算を明確に分けて計算し、その後に連結するのが確実です。

```perl
my $now                 = 2020;
my $start_perl_entrance = 2012;

print "Perl入学式は開講" . ($now - $start_perl_entrance ). "年です\n"; # Perl入学式は開講8年です
```

`( )`で演算の順序を変更することで、正しい結果になります。

---

## 四則演算と文字列連結

### 数値演算と文字列連結は分ける

```perl
my $now                 = 2020;
my $start_perl_entrance = 2012;

my $period = $now - $start_perl_entrance;
print "Perl入学式は開講" . $period . "年です\n"; # Perl入学式は開講8年です
```

計算結果を別の変数に入れてから連結するのも良いでしょう。

---

## 四則演算と文字列連結

### 練習問題(sansuu1.pl)

以下の問題を Perl の四則演算を使って、変数を使わずに解いて表示してください。

- １＋２＋３＋４＋５＋６＋７＋８＋９＋１０

- １００ ー　２００

- １０００円の品物を消費税８%で買った時の税込価格

- 半径５ m の円の面積（円周率は３．１４とする）

---

## 練習問題(sansuu2.pl)

以下の問題を Perl の四則演算を使って解いてください。

`my $pizza = 10` を使って解いて表示してください

- １０個のピザを５人で分けた時の、一人当たりの個数

- １０個のピザを３人で分けた時の、余りの数

---

## 回答 ＆ 休憩 ＆ 質問 ＆ 雑談 タイム<br>（ 5 〜 10 分）

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

---

# コマンドライン<br>引数

---

## コマンドライン引数

### 入力とは？

- これまでは、あらかじめプログラムに書かれた文字や数字を表示してきました。

- プログラムやコンピュータに様々な数値や文字列を与えることを「入力」と言います。

- この「入力」を用いて、プログラムの実行の都度、中身や出力を変えることができます。

- コマンドライン引数は、プログラムが入力を受け取る方法の一つです。

---

## コマンドライン引数

### Perl でコマンドライン引数を受け取る

プログラムに引数を受け取る変数を用意し、`shift` で取り込みます。

```perl
my $foo = shift;  # コマンドライン引数から値を1つ受け取り、変数 $foo に代入する

my $bar = shift;  # コマンドライン引数から値を1つ受け取り、変数 $bar に代入する

print $foo;         # $foo を表示する
print $bar;         # $bar を表示する
```

---

## コマンドライン引数

### Perl でコマンドライン引数を受け取る（ターミナル）

ターミナルの場合には、以下のようにしてコマンドライン引数を設定します。

arg とあるのが引数です。

```bash
$ perl hoge.pl arg
```

複数の引数を受け取る場合には、空白で区切って列挙します。

```bash
$ perl prog.pl "arg1" "arg2"
```

---

## コマンドライン引数

### Perl でコマンドライン引数を受け取る（Wandbox）

Wandbox の場合には、画面左上、バージョン情報の下にある `Runtime options:` の入力欄に「1 つの引数につき 1 行」で入力します。

<img src="https://raw.githubusercontent.com/perl-entrance-org/workshop-basic-online/master/2nd/image/wandbox10.png" align='left'>

---

## コマンドライン引数

### Perl でコマンドライン引数を受け取る

コマンドライン引数：`autumn`

```perl
my $season = shift;   # コマンドライン引数から値を1つ受け取り、
                      # 変数 $season に格納する

print "Alice loves $season\n";    # Alice loves autumn
```

このように、コマンドライン引数によってプログラムの実行結果を変えることが可能です。

---

## コマンドライン引数

### 練習問題 (hello_argument.pl)

コマンドライン引数から名前を読み込み、入力された名前の左側に `Hello` 、右側に `!` を加えて表示するプログラムを作成しよう。

コマンドライン引数：`Taro`

```bash
Hello Taro!
```

---

## コマンドライン引数

### 練習問題 (calc_argument.pl)

コマンドライン引数 から 0 以外の整数を 2 つ読み込み、 それらを四則演算(`+`, `-`, `*`, `/`)した結果を下の例のように表示するプログラム `calc_argument.pl`を作成しよう。

最初のコマンドライン引数に `1` 、次に `2` を入れた場合、以下のように表示されるプログラムです。

```plaintext
1 + 2 = 3
1 - 2 = -1
1 * 2 = 2
1 / 2 = 0.5
```

---

## 回答 ＆ 休憩 ＆ 質問 ＆ 雑談 タイム<br>（ 5 〜 10 分）

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

---

## （コラム）Perl5 の安定板と開発版

- 5.(偶数) が安定版、 5.(奇数) が開発版です。

- 5.(奇数) の開発版をあえてインストールする必要はありません。5.(偶数) の安定版をインストールしましょう。

- 基本的に最新版の一つ手前の安定版までがサポート対象です。

---

## （コラム）Perl5 の安定板と開発版

- 過去のバージョンの Perl が生き残っている現場も多いですが、今なら Perl 5.10 以降だけを考えれば良いです。

- Perl 5.10 以降から 5.32 まで、Perl 初学者にとって大きな違いはあまりありません。

---

# if 文

---

## if 文

### 条件分岐

プログラムは上から下へ順番に処理されていきます。

しかし、変数の状態に応じてプログラムの動きを分岐させることができます。これを **条件分岐** といいます。

条件分岐には様々なパターンがありますが、それらを **真** と **偽** の 2 つの組み合わせで表現します。

真 または 偽 の判断に用いる式を **条件式** といいます。

---

## if 文

### 条件式

条件式、と言っても難しいことではありません。

- 信号が青なら、横断歩道を渡っても良い
- 信号が青でないなら、横断歩道を渡ることはできない

これも立派な条件分岐です。

この場合の条件式は「信号が青であるか、青ではないか」を判断するものです。

---

## if 文

### Perl における真値

条件式の結果は 真 または 偽 となります。

Perl では、次のページの 5 つの値が｢偽｣の値として扱われます。

これ以外の値は、Perl では全て「真」として扱われます。

例示などで「真の値」が必要なときは数字の **1** が利用されます。

---

## if 文

### Perl における偽値

1. 数値｢`0`｣
1. 文字列｢`'0'`｣
1. 文字列｢`''`｣（シングルクォーテーションの連続、間に何もない。空文字ともいう。）
1. 空のリスト｢`()`｣ Perl 入学式では扱わない
1. `undef`（まだ値を設定していないスカラー変数）Perl 入学式では扱わない

---

## if 文

### Perl には true/false がない

他のプログラミング言語では、`true` や `false` という単語に 真 または 偽 の意味を持たせたキーワードとして扱うことがあります。

しかし、Perl は `true` や `false` などの文字で真偽値を表すことはできません。

また特別な意味もありません。

あくまで、Perl では先に述べた 5 つの偽値のみが偽、それ以外はすべて真となります。

---

## if 文

### if 文の構文（擬似プログラム）

Perl の if 文はこのような構造です。

```perl
if ( 条件式 ) {
    条件式の結果が 真 の場合に実行されるブロック;
}
else {
    条件式の結果が 偽 の場合に実行されるブロック;
}
```

- 丸括弧 `( )` の中に条件式を書く

- 波括弧 `{ }` の中に 真・偽 それぞれの場合のプログラムを書く。なお、`{ }` で囲まれた部分を **ブロック** と呼ぶ。

---

## if 文

### if 文の構文（擬似プログラム）

先の青信号の例で例えてみます。

```perl
if ( 信号が青だ ) {
    信号を渡る
}
else {
    信号を渡らない
}
```

---

## if 文

### if 文の構文（Perl のプログラム）

```perl
if ( 1 ) {           # 条件式の結果が明示的な「真」の値である 1
    print "OK\n";    # 条件が｢真｣なので出力される
}
else{
    print "NG\n";    # 実行されない
}
```

---

## if 文

### if 文の構文（Perl のプログラム）

```perl
if ( 0 ) {           # 条件式の結果が「偽」の値である 0
    print "OK\n";    # 実行されない
}
else{
    print "NG\n";    # 条件が｢偽｣なので出力される
}
```

---

## if 文

### 数値の比較演算子

if 文は条件式によって実行結果が変わります。この条件式の中で利用するのが **比較演算子** です。まずは数値の比較演算子を紹介します。

```perl
== # 右辺と左辺が等しいならば「真」
!= # 右辺と左辺が等しくないならば「真」

<  # 右辺より左辺が小さいならば「真」
>  # 右辺より左辺が大きいならば「真」

<= # 右辺が左辺以上ならば「真」
>= # 右辺が左辺以下ならば「真」
```

---

## if 文

## 数値を使った条件分岐の例

```perl
my $foo = 1;
if ( $foo == 1 ) { # <= ここが条件式！

    print "OK\n"; # こちらが表示される
}
else {
    print "NG\n";
}
```

条件式は `( )` の中の `$foo == 1` です。

---

## if 文

## 数値を使った条件分岐の例

`==` は **右辺と左辺が数値として等しければ真** であることを意味します。

条件式内で `$foo` が `1` と等しい場合に「真」となり、`OK` という文字が表示されます。

`$foo` が `1` と等しくない場合は、「偽」となり `else` に処理が移って `NG` という文字が表示されます。

---

## if 文

### よくあるまちがい (1)

```perl
my $foo == 1; # NG

my $bar =  1; # OK
```

数値の比較に用いる `==` と、代入の `=` を間違えています。

この場合、実行すると以下のエラーが出ます。

```perl
Useless use of numeric eq (==) in void context
```

「おまじない」の 3 行を忘れた場合はエラーが出ずに危険です。

---

## if 文

### よくあるまちがい (2)

```perl
my $foo = 1;
if ($foo =  1) { print $foo } # NG
if ($foo == 1) { print $foo } # OK
```

条件式に注目してください。代入の `=` と、数値の比較に用いる `==` を間違えています。

この場合、実行すると以下のエラーが出ます。

```perl
Found = in conditional, should be ==
```

「おまじない」の 3 行を忘れた場合はエラーが出ずに危険です。

---

## 練習問題(big_or_small.pl)

```perl
#!/usr/bin/env perl
use strict;
use warnings;
my $mycard = 10;    # 1から13までの好きな数字を入力しておく
```

トランプの大小判定ゲームを作ってみよう。

1. 変数 `$mycard` に 1 から 13 までの好きな数字（これがトランプの手札）を代入します。

2. `$mycard` の数が 7 より大きければ `BIG` と表示します。

3. それ以外(7 以下)であれば、 `SMALL` と表示します。

---

## 回答 ＆ 休憩 ＆ 質問 ＆ 雑談 タイム<br>（ 5 〜 10 分）

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

---

## if 文

### 文字列の比較演算子

文字列を比較する場合は `eq` や `ne` といった単語を用いた比較演算子を使います。

```perl
eq  # equal
ne  # not equal

gt  # greater than
ge  # greater equal

lt  # less than
le  # less equal
```

---

## if 文

### 文字列の比較演算子

文字列を比較する場合は `eq` や `ne` といった単語を用いた比較演算子を使います。

「文字列の大小」とは、文字列の長さではありません。

辞書順に並べた時に前にくるものが小さく、後ろにくるものが大きいという意味です。

---

## if 文

### 文字列を使った条件分岐

条件式に注目してください。

```perl
my $foo = 'hello';

if ( $foo eq 'hello' ) { # <= ここが条件式

    print "OK\n";   # こちらが表示される
}
else {
    print "NG\n";
}
```

文字列を比較しているので、 `==` ではなく `eq`を用いています

---

## 練習問題(question_word_arg.pl)

```perl
#!/usr/bin/env perl
use strict;
use warnings;
my $answer = 'perl';    # 好きな文字を入力しておく
```

簡単な文字当てゲーム `question_word.pl`を作成しよう。

1. コマンドライン引数から文字列を一つ入力します。

1. その文字列が `$answer` と一致したら`OK`と表示します。

1. 外れたら `NG` と表示します。

---

## 回答 ＆ 休憩 ＆ 質問 ＆ 雑談 タイム<br>（ 5 〜 10 分）

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

---

## if 文

### 数値 と 文字列 の比較を混合させる<br>アンチパターン

```perl
print "eq で評価すると、";
if ( '51.0' eq 51 ) {
    print "文字列'51.0' と 数値 51 は等しい\n";
}
else {
    print "文字列'51.0' と 数値 51 は等しくない\n"; # こちらが表示される
}
```

`eq` は文字列を比較するときに使う比較演算子です。

このため、条件式の左辺・右辺は文字列として **異なる** と判断されます。

---

## if 文

### 数値 と 文字列 の比較を混合させる<br>アンチパターン

```perl
print "== で評価すると、";
if ( '51.0' == 51 ) {
    print "文字列'51.0' と 数値 51 は等しい\n"; # こちらが表示される
}
else {
    print "文字列'51.0' と 数値 51 は等しくない\n";
}
```

条件式の比較演算子を `eq` から `==` に変更すると、先ほどとは逆の結果となります。数値として比較されるためです。

---

## if 文

### 数値 と 文字列 の比較を混合させる<br>アンチパターン

条件式を書くときは、

- 数値を比較したい

- 文字列を比較したい

どちらなのかを認識した上で、対応した比較演算子を選ぶことが重要です。

---

## if 文

### 複数の条件分岐

`elsif`を使うことで、分岐条件を増やすことができます。

`else if` ではなく、 **`elsif`** です。

---

## if 文

### 複数の条件分岐

```perl
my $foo = 1;

if ( $foo == 1 ) {
    print '$foo is One' . "\n"; # これが表示される
}
elsif ( $foo == 2 ) {
    print '$foo is Two' . "\n";
}
elsif ( $foo == 3 ) {
    print '$foo is Three' . "\n";
}
else {
    print '$foo is not even One, even Two, even Three' . "\n";
}
```

---

## 練習問題(big_seven_small_arg.pl)

先に作ったトランプの大小判定ゲームを改良してみよう。

1. 変数 `$mycard` に、コマンドライン引数を利用して 1 から 13 までの好きな数字を直接入力できる様に改造しよう。

2. `$mycard` の数が 7 より大きければ `BIG!` と表示します。

3. `$mycard` の数が 7 と等しければ、 `Seven!` と表示します。

4. `$mycard` の数が 7 未満であれば、 `SMALL!` と表示します。

---

## 回答 ＆ 休憩 ＆ 質問 ＆ 雑談 タイム<br>（ 5 〜 10 分）

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

---

# 論理演算子

---

## 論理演算子

### 論理演算子 AND, OR

`if文` の条件式でより複雑な条件を扱いたいときは **論理演算子** を用いて複数の条件を連結します。

```perl
&& # かつ

|| # または
```

- `&&` 演算子は｢かつ・AND｣の意味を持つ。 `&&` の両辺の条件が 真 となるときのみ 真 となる。

- `||` 演算子は｢または・OR｣の意味を持つ。 `||` 両辺のうち、片方が 真 ならば 真 となる。

---

## 論理演算子

### AND

条件式に注目してください。

```perl
my $foo = 10;

if ( $foo > 0 && $foo % 2 == 0 ) {  # $fooが 0 より大きい かつ 2 で割り切れる
    print "&& : OK\n";    # こちらが表示される
}
else{
    print "&& : NG\n"
}
```

---

## 論理演算子

### AND

先の条件式の抜粋です。

```perl
my $foo = 10;

if ( $foo > 0 && $foo % 2 == 0 ) {
```

1. `&&` の左辺にある `$foo` が 0 より大きいので、`$foo > 0` は 真。
1. `&&` の右辺にある `$foo` を 2 で割った剰余は 0 なので、`$foo % 2 == 0` も 真。
1. 「1. かつ 2. が 真」の条件を満たすので、条件式は 真 となり、`&&: OK` と表示される。

---

## 論理演算子

### OR

条件式に注目してください。

```perl
my $foo = 10;

if ( $foo > 0 || $foo % 2 == 1 ) {  # $fooが 0 より大きい または 2 で割り切れない
    print "|| : OK\n";     # こちらが表示される
}
else{
    print "&& : NG\n"
}
```

---

## 論理演算子

### OR

先の条件式の抜粋です。

```perl
my $foo = 10;

if ( $foo > 0 || $foo % 2 == 1 ) {
```

1. `||` の左辺にある `$foo` が 0 より大きいので、`$foo > 0` は 真。

1. 「1. または 2. が 真」の条件のうち、「1. が 真 」を満たすので、条件式は 真 となり、`||: OK` と表示される。右辺は比較されない。

---

## 論理演算子

### 3 つ以上の値を比較したい場合

```perl
my $small  = 10;
my $medium = 20;
my $large  = 30;

if ( $small < $medium <= $large ) { # Perl 5.30までのバージョンではエラーになる
    print "小さい順に並んでます\n";
}
```

---

## 論理演算子

### 3 つ以上の値を比較したい場合

```perl
$small < $medium < $large
```

上記のように、に 3 つ以上の値を同時に比較することはできません。

下記のようなエラーとなります。

    syntax error at sample.pl line 5, near "$medium <"

ただし、2020 年にリリースされた Perl 5.32 より、3 つ以上の値を同時に比較することが可能になりました。

---

## 論理演算子

### 3 つ以上の値を比較したい場合(Perl 5.32 以降)

```perl
my $small  = 10;
my $medium = 20;
my $large  = 30;

if ( $small < $medium <= $large ) { # 不等号が2種類ある
    print "小さい順に並んでます\n";
}
```

不等号をはじめ、演算子には[優先順位](https://perldoc.jp/docs/perl/perlop.pod)があります。

上記の例の場合、以下のように解釈されます。

```perl
$small < $medium  && $medium <= $large
```

---

## 論理演算子

### 3 つ以上の値を比較したい場合(Perl 5.30 以前)

```perl
my $small  = 10;
my $medium = 20;
my $large  = 30;

if ( $small < $medium && $medium <= $large ) {
    print "小さい順に並んでます\n";
}
```

`$small < $medium` かつ `$medium <= $large` というように、論理演算子を使って比較することも可能です。

---

## 練習問題(question_num_arg.pl)

```perl
#!/usr/bin/env perl
use strict;
use warnings;
my $answer = 10;    # 好きな値を入力しておく
```

簡単な数当てゲーム `question_num_arg.pl`を作成しよう。

コマンドライン引数から数字を一つ入力し、その数字とプログラム中の `$answer` とを比較して、次ページの条件に従った表示をするプログラムを作ってください。

---

## 練習問題(question_num_arg.pl)

- `$answer` と一致したら `OK`

- `$answer` より大きければ `too big`

- `$answer` より小さければ `too small`

- （余裕があれば）入力した値が `$answer`から -5 〜 +5 の範囲内(例えば、`$answer`が 10 なら 5 〜 15)の場合、`near`と表示する。

---

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

---

## （コラム）TMTOWTDI

Perl のスローガン、「ティムトゥーディー」と読みます。

" There's more than one way to do it. " の頭文字をとったもの。

意味は「やり方はひとつじゃない」。

---

## （コラム）TMTOWTDI

Larry は「プログラミング言語は、いろんな対象をシンプルに記述する為に、ある程度複雑でなければならない」と信じています。

Perl は同じ処理を、様々な書き方で表すことができます。

これについては、きっとこれからの Perl 入学式のカリキュラムの中で実際に体験することになります。

---

## お疲れ様でした!

第 1 回よりも格段に増えた長い時間、Perl 入学式第 2 回お疲れ様でした！

是非 Perl 入学式の Discord #雑談部屋 でサポーターや参加者の皆さんと交流しましょう。

不明点も Discord #雑談部屋 で是非質問してください。

第 3 回以降の参加もお待ちしております!

---

## 次回（以降）予告

- スカラー変数の集合である「配列」

- 配列の操作

- くりかえし「for 文」

- ハッシュ

- 正規表現

- リファレンス

- サブルーチン
